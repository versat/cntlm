[Setup]
AppId={{4D753458-961F-45DA-B5E3-7B44D4E368B4}
AppName=Cntlm
AppVerName=Cntlm v$VERSION
AppCopyright=Copyright (C) 2007-2012 David Kubicek
AppContact=cntlm@awk.cz
AppPublisher=David Kubicek
AppPublisherURL=http://cntlm.sf.net/
LicenseFile=LICENSE.txt
ArchitecturesAllowed=x64

DefaultDirName={pf}\Cntlm
DefaultGroupName=Cntlm
SetupIconFile=cntlm.ico
UninstallDisplayIcon={app}\cntlm.ico
Uninstallable=yes
OutputBaseFileName=cntlm-$VERSION-win64
OutputDir=..

[Files]
Source: "cntlm.exe"; DestDir: "{app}"
Source: "cygrunsrv.exe"; DestDir: "{app}"
Source: "cygwin1.dll"; DestDir: "{app}"
Source: "cntlm.ini"; DestDir: "{app}"; Flags: uninsneveruninstall confirmoverwrite
Source: "cntlm_manual.pdf"; DestDir: "{app}"
Source: "LICENSE.txt"; DestDir: "{app}";
Source: "README.txt"; DestDir: "{app}";
Source: "Cntlm Homepage.url"; DestDir: "{app}"
Source: "Software Updates.url"; DestDir: "{app}"
Source: "Support Website.url"; DestDir: "{app}"

[Run]
Filename: "{app}\cygrunsrv.exe"; StatusMsg: "Stopping Cntlm service..."; Parameters: " --stop cntlm"
Filename: "{app}\cygrunsrv.exe"; StatusMsg: "Removing Cntlm service..."; Parameters: " --remove cntlm"
Filename: "{app}\cygrunsrv.exe"; StatusMsg: "Installing Cntlm service..."; Parameters: "--install cntlm -s KILL -t auto -p ""{app}\cntlm.exe"" -d ""Cntlm Authentication Proxy"" -f ""HTTP Accelerator"" -a -f"

[Icons]
Name: "{group}\cntlm.ini"; Filename: "{app}\cntlm.ini"
Name: "{group}\Start Cntlm Authentication Proxy"; Filename: "{sys}\net.exe"; Parameters: "start cntlm"; WorkingDir: {app}
Name: "{group}\Stop Cntlm Authentication Proxy"; Filename: "{sys}\net.exe"; Parameters: "stop cntlm"; WorkingDir: {app}
Name: "{group}\Tools\Uninstall Cntlm"; Filename: "{uninstallexe}"
Name: "{group}\Tools\Cntlm Homepage"; Filename: "{app}\Cntlm Homepage.url"
Name: "{group}\Tools\Software Updates"; Filename: "{app}\Software Updates.url"
Name: "{group}\Tools\Support Website"; Filename: "{app}\Support Website.url"
Name: "{group}\Tools\PDF configuration manual"; Filename: "{app}\cntlm_manual.pdf"

[UninstallRun]
Filename: "{app}\cygrunsrv.exe"; StatusMsg: "Stopping Cntlm service..."; Parameters: " --stop cntlm"
Filename: "{app}\cygrunsrv.exe"; StatusMsg: "Removing Cntlm service..."; Parameters: " --remove cntlm"
